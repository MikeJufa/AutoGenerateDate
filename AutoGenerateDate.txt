/******Main Tab******/
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$#,##0.00;-$#,##0.00';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='M/D/YYYY';
SET TimestampFormat='M/D/YYYY h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:µ;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';

LET vCY = Year(Today());
LET vLY = Year(Today())-1;
LET vCM = date(monthstart(today()),'MMM-YY');
LET vLM = date(addmonths(today(),-1),'MMM-YY');

/******Sub Tab******/
//==============================================================================
// Subroutine to Generate Calendar
//==============================================================================

SUB CalendarFromField(_field, _calendar, _prefix)
[$(_calendar)]:
//Generate Final Calendar
Load
     [$(_field)]
    ,Year([$(_field)]) as [$(_prefix)Year]
    ,'Q'&Ceil(Month([$(_field)])/3) as [$(_prefix)Quarter]
    ,Month([$(_field)]) as [$(_prefix)Month]
    ,Date(MonthStart([$(_field)]),'MMM-YY') as [$(_prefix)YearMonth]
    ,Date(MonthStart([$(_field)]),'YYYYMM') as [$(_prefix)YearMonthNum]
    ,Week([$(_field)]) as [$(_prefix)Week]
    ,WeekDay([$(_field)]) as [$(_prefix)WeekDay]
    ,Day([$(_field)]) as [$(_prefix)Day]
    ,WeekStart([$(_field)]) as [$(_prefix)WeekOf]
    ,if(DayNumberOfYear([$(_field)])<=DayNumberOfYear(Today()), 1, 0) as [$(_prefix)IsInYTD]
    ,if(Day([$(_field)])<=Day(Today()), 1, 0) as [$(_prefix)IsInMTD]
;
//Generate a range of dates between min and max dates.
Load
     Date(DateMin + IterNo()) as [$(_field)]                       //Link Field
While DateMin + IterNo() <= DateMax
;
//Find min and max of date of field values.
Load
     Min(datefield)-1 as DateMin
    ,Max(datefield) as DateMax
;
//Load date field values.
Load
     FieldValue('$(_field)', RecNo()) as datefield
AutoGenerate FieldValueCount('$(_field)');

END SUB
//updated on April 2, 2018

/*****Raw_Calendar Tab*****/
LET vDateMin = num(MakeDate(2012,1,1));
LET vDateMax = Floor(MonthEnd(Today()));
LET vDateToday = num(Today());

SET vFM=4;

Calendar_Sales:
Load
	*
	,Month(OrderDate) as Month
    ,Year(OrderDate) as Year
    ,MonthName(OrderDate) as MonthName
    ,Week(OrderDate) as Week
    ,Date(MakeDate(Year(OrderDate),Month(OrderDate)),'YYYYMM') as YearMonth
    ,Num(Date(MakeDate(Year(OrderDate),Month(OrderDate)),'YYYYMM')) as YearMonthNum
    ,Today()-Num(OrderDate) as DaysAgo
    ,12*(Year(Today())-Year(OrderDate)) + Month(Today()) - Month(OrderDate) as MonthsAgo
;
Load Distinct
	 Date($(vDateMin)+ IterNo()-1) as OrderDate    
    ,num(Rand()*1000,'#,##0.00') as Sales
    ,num(Rand()*100,'#,##0') as Customer
AutoGenerate 1
While $(vDateMin)+IterNo()-1 <= $(vDateMax)
;

CALL CalendarFromField('OrderDate', 'OrderCalendar', 'Order_'); 